apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.secrets");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

ext {
    supportLibraryVersion = '27.0.2'
    butterknifeVersion = "8.8.1"
    leakCanaryVersion = "1.5.4"
    dagger2Version = "2.11"
    moxyVersion = "1.5.3"
}

dependencies {

    //Triage this:
    implementation group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version: '1.9.6'
    implementation group: 'org.ccil.cowan.tagsoup', name: 'tagsoup', version: '1.2.1'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.7.3'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.1.2'

    //Support lib
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:preference-v7:$supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"

    //Design
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.9'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.6.0'
    implementation 'com.github.Aspsine:SwipeToLoadLayout:1.0.4'

    //Fragment navigation
    implementation 'com.ncapdevi:frag-nav:2.4.0'
    implementation 'ru.terrakok.cicerone:cicerone:2.1.0'

    //Crash reporting
    implementation 'ch.acra:acra:4.9.2'

    //Leaks detection
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    //Logging
    implementation 'com.jakewharton.timber:timber:4.6.0'

    //Views binding
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    //DI
    implementation "com.google.dagger:dagger:$dagger2Version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger2Version"

    //Reactive
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'

    //Http stack
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'

    //MVP
    implementation "com.arello-mobile:moxy:$moxyVersion"
    implementation "com.arello-mobile:moxy-app-compat:$moxyVersion"
    annotationProcessor "com.arello-mobile:moxy-compiler:$moxyVersion"

}
configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
}

android {
    useLibrary 'org.apache.http.legacy'

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 27
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "ua.in.quireg.chan"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 2000
        versionName "2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "CAPTCHA_API_PUBLIC_KEY", keystoreProperties['CAPTCHA_API_PUBLIC_KEY'])
        buildConfigField("String", "CAPTCHA_API_PRIVATE_KEY", keystoreProperties['CAPTCHA_API_PRIVATE_KEY'])
        buildConfigField("long", "BUILD_TIMESTAMP", System.currentTimeMillis() + "L")
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/ASL2.0'
    }

    buildTypes {
        debug {
            minifyEnabled false
//            signingConfig signingConfigs.config
        }
        release {
            minifyEnabled false
//            signingConfig signingConfigs.config
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}